run {
CROSSCHECK BACKUPSET;
CROSSCHECK COPY;
# Backup database
BACKUP AS COMPRESSED BACKUPSET DEVICE TYPE DISK FILESPERSET=1
FORMAT 'D:\Backup\df_%u.dbf' TAG 'full'
INCREMENTAL LEVEL 0 DATABASE;
# Backup Archived Logs
sql 'alter system archive log current';
BACKUP DEVICE TYPE DISK
FORMAT 'D:\Backup\arch_%e_%h_%u.arc' TAG 'arch'
ARCHIVELOG ALL NOT BACKED UP DELETE ALL INPUT;
# Current controlfile
BACKUP DEVICE TYPE DISK CURRENT CONTROLFILE
FORMAT 'D:\Backup\ctrl_%d_%I_%u.ctl' TAG 'ctrl';
# SPFILE
BACKUP DEVICE TYPE DISK SPFILE
FORMAT 'D:\Backup\spfile_%d_%u.ora' TAG 'spfile';
DELETE NOPROMPT OBSOLETE;
DELETE NOPROMPT EXPIRED BACKUPSET;
DELETE NOPROMPT EXPIRED COPY;
DELETE NOPROMPT EXPIRED ARCHIVELOG ALL;
}

